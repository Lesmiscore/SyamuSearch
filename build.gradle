buildscript {
    ext.kotlin_version = '1.2.20'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

group 'com.nao20010128nao'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile project(":indexer")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task("allJars")

[
    "indexer":"com.nao20010128nao.SyamuSearch.IndexerKt",
    "combiner":"com.nao20010128nao.SyamuSearch.CombinerKt",
    "fastIndexer":"com.nao20010128nao.SyamuSearch.FastIndexerKt",
    "finder":"com.nao20010128nao.SyamuSearch.Finder"
].forEach{k,v->
    task ("${k}Jar",type: Jar) {
        baseName = "$k"
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
        manifest {
            attributes 'Implementation-Title': k, 'Implementation-Version': version
            attributes 'Main-Class': v
        }
    }
    allJars.dependsOn "${k}Jar"
}

